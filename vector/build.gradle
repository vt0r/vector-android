apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    defaultConfig {
        applicationId "im.vector"
        minSdkVersion 16
        targetSdkVersion 23
        // use the version code
        versionCode rootProject.ext.versionCodeProp
        versionName rootProject.ext.versionNameProp
    }

    final Console console = System.console();
    signingConfigs {
        releaseSigning {
            storeFile file(System.getenv("ANDROID_KEYSTORE"))
            keyAlias System.getenv("ANDROID_KEYALIAS")
            storePassword "FakePassword"
            keyPassword "FakePassword"
        }
    }

    task checkPasswords << {
        def keyStorePass = new String(console.readPassword("\nKeystore password: "))
        def keyPass  = new String(console.readPassword("Key password: "))
        android.signingConfigs.releaseSigning.storePassword = keyStorePass
        android.signingConfigs.releaseSigning.keyPassword = keyPass
    }

    tasks.whenTaskAdded { theTask ->
        if (theTask.name.equals("packageAppRelease")) {
            theTask.dependsOn "checkPasswords"
        }
    }

    buildTypes {
        debug {
            resValue "string", "git_revision", "\"${gitRevision()}\""
            resValue "string", "git_revision_unix_date", "\"${gitRevisionUnixDate()}\""
            resValue "string", "git_revision_date", "\"${gitRevisionDate()}\""
            minifyEnabled false
        }

        release {
            resValue "string", "git_revision", "\"${gitRevision()}\""
            resValue "string", "git_revision_unix_date", "\"${gitRevisionUnixDate()}\""
            resValue "string", "git_revision_date", "\"${gitRevisionDate()}\""
            shrinkResources true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseSigning
        }

        matrixorg.initWith(buildTypes.release)
        matrixorg {
            signingConfig signingConfigs.releaseSigning
        }
    }

    productFlavors {
        app {
            applicationId "im.vector.alpha"
            // use the version name
            versionCode rootProject.ext.versionCodeProp
            versionName rootProject.ext.versionNameProp
            resValue "string", "allow_gcm_use", "true"
            resValue "string", "allow_ga_use", "true"
            resValue "string", "flavor_description", ""
        }

        appfdroid {
            applicationId "im.vector.alpha"
            // use the version name
            versionCode rootProject.ext.versionCodeProp
            versionName rootProject.ext.versionNameProp
            resValue "string", "allow_gcm_use", "false"
            resValue "string", "allow_ga_use", "false"
            resValue "string", "flavor_description", "FDroid version"
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    repositories {
        flatDir {
            dir 'libs'
        }

        maven {
            url "https://s3.amazonaws.com/repo.commonsware.com"
        }
    }
}

def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}

def gitRevisionUnixDate() {
    def cmd = "git show -s --format=%ct HEAD^{commit}"
    return cmd.execute().text.trim()
}

def gitRevisionDate() {
    def cmd = "git show -s --format=%ci HEAD^{commit}"
    return cmd.execute().text.trim()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'me.leolin:ShortcutBadger:1.1.2@aar'

    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'

    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'io.pristine:libjingle:9690@aar'

    compile 'com.commonsware.cwac:anddown:0.2.+'

    // bug report encryption
    compile 'net.lingala.zip4j:zip4j:1.3.1'

    /************* Matrix SDK management **************/
    // use the matrix SDK as external lib
    compile(name: 'matrix-sdk', ext: 'aar')

    // use the matrix SDK as a part of the project
    // you have to uncomment some lines in settings.gradle
    //compile project(':matrix-sdk')

    /************* flavors management **************/

    // app flavor only
    appCompile 'com.google.android.gms:play-services-gcm:8.1.0'
    appCompile 'com.google.android.gms:play-services-analytics:8.1.0'

    // fdroid flavor only
}

